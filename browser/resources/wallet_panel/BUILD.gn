# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_web_components_manifest = "preprocessed_gen_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = true
  source = "$target_gen_dir/wallet_panel_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/wallet_panel_resources.h",
    "grit/wallet_panel_resources_map.cc",
    "grit/wallet_panel_resources_map.h",
    "wallet_panel_resources.pak",
  ]

  output_dir = "$root_gen_dir/brave"
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  resource_ids = "//brave/browser/resources/resource_ids"
}

generate_grd("build_grd") {
  input_files = [ "wallet_panel.html" ]
  input_files_base_dir = rebase_path(".", "//")
  deps = [
    ":preprocess",
    ":preprocess_mojo",
    ":preprocess_web_components",
    "//brave/components/brave_wallet_ui:brave_wallet_panel_ui",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_web_components_manifest",
    "$target_gen_dir/$preprocess_mojo_manifest",
  ]
  grd_prefix = "wallet_panel"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "wallet_panel_api_proxy.js" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//brave/browser/ui/webui/wallet_panel:mojo_bindings_js" ]
  in_folder = "$root_gen_dir/brave/browser/ui/webui/wallet_panel/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "wallet_panel.mojom-lite.js" ]
}

preprocess_if_expr("preprocess_web_components") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_web_components_manifest"
  in_files = [ "app.js" ]
}

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":app",
    ":wallet_panel_api_proxy",
  ]
}

js_library("app") {
  deps = [
    ":wallet_panel_api_proxy",
    "//third_party/polymer/v3_0/components-chromium/iron-selector:iron-selector",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_icon_button:cr_icon_button.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("wallet_panel_api_proxy") {
  deps = [
    "//brave/browser/ui/webui/wallet_panel:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}

html_to_js("web_components") {
  js_files = [ "app.js" ]
}
